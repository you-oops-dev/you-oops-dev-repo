# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libcorkipset
pkgver=1.1.1
_debver=20150311-8
pkgrel=6
pkgdesc="A small C helper library for storing sets of IPv4 and IPv6 addresses"
url="https://github.com/rogers0/libcorkipset"
arch=('x86_64')
license=('BSD')
depends=('libcork')
makedepends=('cmake3' 'check' 'git')
checkdepends=('python')
options=('staticlibs' '!lto')
options+=('!debug')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rogers0/$pkgname/archive/debian/$pkgver+$_debver.tar.gz"
	"fix-gcc-10.patch")
sha512sums=('5bbce2727da1665e734981959ecb253b4052d8cc6c426fded7e1678b6e2505bc9f4c0ab3cd3b6a03f834f5e20bd1d670cc19f7491fe0ad5b7cb772e7309f5009'
            'f0441ccc3800f450fa752ae4160dea0686fff2b5b255cfcb91bba8561301519fcc327125c2731ecbb909446ee6973c99d36d0d90f080bffd2d4a422f307e729d')
prepare() {
  cd $pkgname-debian-$pkgver-$_debver
    mkdir -pv build
    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch -Np1 < "../$src"
    done

	sed -e 's%#include <ipset%#include <libcorkipset%' \
		-e 's%#include "ipset%#include "libcorkipset%' \
		-i include/ipset/*.h */*/*/*.c */*/*/*.c.in */*/*.c */*.c
	mv include/ipset include/libcorkipset


}

build() {
  cd $pkgname-debian-$pkgver-$_debver/build
  /opt/cmake3/bin/cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make -s
}

check() {
  cd $pkgname-debian-$pkgver-$_debver/build
  make -s test
}

package() {
  cd $pkgname-debian-$pkgver-$_debver/build
  make -s DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir"/$pkgname-debian-$pkgver-$_debver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv -fv "$pkgdir"/usr/lib/libipset.so.1.1.0 "$pkgdir"/usr/lib/libcorkipset.so.1.1.0
  rm -fv "$pkgdir"/usr/lib/libipset.so*
  ln -svf libcorkipset.so.1.1.0 "$pkgdir"/usr/lib/libcorkipset.so
  ln -svf libcorkipset.so.1.1.0 "$pkgdir"/usr/lib/libcorkipset.so.1
  ln -svf libcorkipset.so.1.1.0 "$pkgdir"/usr/lib/libipset.so.1

}
